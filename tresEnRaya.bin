//TRES EN RAYA
//
// ** ** **
// ** ** **
// ** ** **
//
// ** O **
// ** O **
// ** O **
//
// U ** **
// ** U **
// ** ** U

//Función que muestra el tablero que se le pasa como parámetro
Funcion MostrarTablero(tableroJuego, jugada)
	//1. Definir e inicializar variables
	Definir filas, columnas Como Entero
	filas = 0
	columnas = 0
	
	//2. Letrero
	Escribir ""
	Escribir " --------------------------------"
	Escribir "| TABLERO - JUGADA ", jugada + 1 " |"
	Escribir " --------------------------------"
	Escribir ""
	
	//3. Mostrar el tablero de juego
	para columnas = 0 Hasta 2 Con Paso 1
		para filas = 0 hasta 2 Con Paso 1
			Escribir tableroJuego[columnas, filas], ' ' Sin Saltar
		FinPara
		Escribir ""
	FinPara
FinFuncion

//Función que revisa el tablero que se le pasa como parámetro, comprobando si hay algún ganador
Funcion hayGanador = ComprobarGanador(tableroJuego)
	//1. Definir e inicializar variables
	definir hayGanador Como Logico
	Definir i Como Entero
	hayGanador = Falso
	i = 0
	
	//2. Comprobamos si hay alguna fila completa con U's o O's, que no sean "**"
	Para i = 0 Hasta (3-1) Con Paso 1 Hacer
		Si (tableroJuego[i,0] <> "***") Y (tableroJuego[i,0] = tableroJuego[i,1]) Y (tableroJuego[i,1] = tableroJuego[i,2]) Entonces
			hayGanador = Verdadero
		FinSi
	FinPara
	//3. Comprobamos si hay alguna columna completa con X's o O's, que no sean "**"
	Para i = 0 Hasta (3-1) Con Paso 1 Hacer
		Si (tableroJuego[0,i] <> "***") Y (tableroJuego[0,i] = tableroJuego[1,i]) Y (tableroJuego[1,i] = tableroJuego[2,i]) Entonces
			hayGanador = Verdadero
		FinSi
	FinPara
	//4. Comprobamos si alguna de las dos diagonales está completa con X's o O's, que no sean "**"
	Si (tableroJuego[0,0] <> "***") Y (tableroJuego[0,0] = tableroJuego[1,1]) Y (tableroJuego[1,1] = tableroJuego[2,2]) Entonces
		hayGanador = Verdadero
	FinSi
	Si (tableroJuego[0,2] <> "***") Y (tableroJuego[0,2] = tableroJuego[1,1]) Y (tableroJuego[1,1] = tableroJuego[2,0]) Entonces
		hayGanador = Verdadero
	FinSi
	
FinFuncion

//Juego de las Tres en Raya
Algoritmo TresEnRaya
	//1. Definir e inicializar variables
	definir filas, columnas, jugador, filaJugador, columnaJugador, numeroJugada, columnaPc, filaPc Como Entero
	Definir tablero Como Caracter
	Definir hayGanador, posicionCorrecta Como logico
	Dimension tablero[3,3]
	filas = 0 
	columnas = 0
	jugador  = 0
	numeroJugada = 0
	filaJugador = 0
	filaPc = 0 
	columnaPc = 0
	columnaJugador = 0
	hayGanador = Falso
	posicionCorrecta = Falso
	
	//1.1. Inicializar las posiciones del tablero con texto "**"
	para columnas = 0 hasta 2 Con Paso 1
		para filas = 0 hasta 2 Con Paso 1
			tablero[columnas, filas] = '***'
		FinPara
	FinPara
	//2. Empieza el juego
	Escribir 'Inicia el juego'
	Escribir ''
	Esperar 1 Segundos
	
	//2.1. Comenzamos mostrando el tablero vacío
	para columnas = 0 Hasta 2 Con Paso 1
		para filas = 0 hasta 2 Con Paso 1
			Escribir tablero[columnas, filas], ' ' Sin Saltar
		FinPara
		Escribir  '' 
	FinPara
	Escribir ''
	
	jugador = azar(2) //0 --> Ordenador; 1 --> Usuario
	
	Mientras (numeroJugada < 9) Y (hayGanador = Falso) Hacer
		//2.2. En el caso de que sea el turno del jugador... (el usuario pone "U")
		si jugador == 1 Entonces
			Escribir 'Elige una fila (0-2)'
			leer filaJugador
			Escribir 'Ahora elige una columna (0-2)'
			leer columnaJugador
			//2.2.1. Pedir al usuario que elija una fila y una columna, hasta que introduzca una posición vacía (tablero[fila,columna] = "**")
			//y poner la ficha del jugador (tablero[fila,columna] = "U")

			si (tablero[columnaJugador, filaJugador] == '***') Entonces
				tablero[columnaJugador, filaJugador] = ' U '
			SiNo
				Mientras (tablero[columnaJugador, filaJugador] <> '***') Hacer
					Escribir 'El espacio ya está ocupado, elige otra fila'
					leer filaJugador
					Escribir 'Ahora elige otra columna'
					leer columnaJugador
				FinMientras
				tablero[columnaJugador, filaJugador] = ' U '
			FinSi
		FinSi
		
		//2.3. En el caso de que sea el turno del ordenador... (el ordenador pone "O")
		si jugador = 0 Entonces
			Escribir 'Turno del ordenador'
			Mientras posicionCorrecta = Falso 
				columnaPc = azar(3)
				filaPc = azar(3)
				si tablero[columnaPc, filaPc] == '***'
					tablero[columnaPc, filaPc] = ' O '
					posicionCorrecta = Verdadero
				FinSi
			FinMientras
			posicionCorrecta = Falso
		FinSi
		
		//2.4. Mostramos el tablero después de poner la ficha
		MostrarTablero(tablero, numeroJugada)
		Escribir ' 	'
		//2.5. Comprobamos si ha ganado el jugador que acaba de poner ficha, y si es así, lo decimos y terminamos el juego (función ComprobarGanador(tablero))
		hayGanador = ComprobarGanador(tablero)
		Si (hayGanador = Verdadero) Y (jugador = 0) Entonces
			Escribir "--- HAS PERDIDO... ---"
			numeroJugada = 9 //Forzamos acabar el juego
		SiNo
			Si (hayGanador = Verdadero) Y (jugador = 1) Entonces
				Escribir "--- HAS GANADO!! ---"
				numeroJugada = 9 //Forzamos acabar el juego
			FinSi
		FinSi
		//2.6. Cambiamos de jugador (Si acaba de jugar el ordenador, ahora le toca al jugador; y viceversa)
		si jugador == 0 Entonces
			jugador = 1
		SiNo
			jugador = 0
		FinSi
		
		numeroJugada = numeroJugada + 1
		Esperar 1 Segundos
	FinMientras
	
	//3. En el caso de que no haya habido un ganador, mostrar que hemos empatado
	si hayGanador == Falso Entonces
		Escribir 'EMPATE'
	FinSi
FinAlgoritmo
